BEGIN TEMPLATE score
    SETTING align pivot
    SETTING item_direction "vertical"
    SETTING cell_direction "horizontal"
    DOUBLE bRadius
    DOUBLE cCode
    INT shadow
    INT hide_dead

    STRING bg_svg
    bg_svg = "<svg "
    bg_svg = bg_svg & " width='" & width & "'"
    bg_svg = bg_svg & " height='" & height & "'"
    bg_svg = bg_svg & " viewBox='" & 0
    bg_svg = bg_svg & " " & 0
    bg_svg = bg_svg & " " & width
    bg_svg = bg_svg & " " & height & "'>"
    bg_svg = bg_svg & "<rect "
    bg_svg = bg_svg & " width='" & width & "' "
    bg_svg = bg_svg & " height='" & height & "' "
    bg_svg = bg_svg & " rx='" & bRadius & "' "
    bg_svg = bg_svg & " fill='#ffffff' "
    bg_svg = bg_svg & " stroke-width='0' "
    bg_svg = bg_svg & " y='" & bWidth & "' "
    bg_svg = bg_svg & " x='" & bWidth & "' "
    bg_svg = bg_svg & "/></svg>"

    STRING macguffin_bar
    macguffin_bar = "<svg width='2.5'"
    macguffin_bar = macguffin_bar & " height='" & height & "'>"
    macguffin_bar = macguffin_bar & "<rect width='2.5'"
    macguffin_bar = macguffin_bar & " height='" & height & "'"
    macguffin_bar = macguffin_bar & " x='0' y='0' fill='white' /></svg>"

    DOUBLE indent
    indent = width * 0.1

    INT dir
    DOUBLE macguffin_offset
    macguffin_offset = DOUBLE (width + 0.8)
    IF direction == "horizontal reverse"
        dir = 1
        macguffin_offset = -3.3
    END IF

    INT hide_when_dead
    IF hide_dead
        hide_when_dead = 1
    END IF

ON_UPDATE

    IF hide_when_dead == 1 && common_game_data.self_alive == "false"
        discard
    END IF

    INT main_score
    INT round_score
    STRING team_color
    STRING team_color_dark
    INT flag_state
    INT has_macguffin
    has_macguffin = 0
    IF team == "own"
        main_score = game_data.own_team.team_score
        round_score = game_data.own_team.game_score
        team_color = game_data.own_team.color
        team_color_dark = game_data.own_team.color_dark
        flag_state = game_data.own_team.team_flag_state
        IF game_data.own_team.team_has_macguffin == "true"
            has_macguffin = 1
        END IF
        IF game_data.game_mode == "wipeout" || game_data.game_mode == "ca"
            main_score = game_data.own_team.game_score
        END IF
        IF game_data.game_mode == "extinction"
            main_score = game_data.own_team.game_score
        END IF
    ELSE 
        main_score = game_data.enemy_team.team_score
        round_score = game_data.enemy_team.game_score
        team_color = game_data.enemy_team.color
        team_color_dark = game_data.enemy_team.color_dark
        flag_state = game_data.enemy_team.team_flag_state
        IF game_data.enemy_team.team_has_macguffin == "true"
            has_macguffin = 1
        END IF
        IF game_data.game_mode == "wipeout" || game_data.game_mode == "ca"
            main_score = game_data.enemy_team.game_score
        END IF
        IF game_data.game_mode == "extinction"
            main_score = game_data.enemy_team.game_score
        END IF
    END IF

    # ====================================
    # MAIN SCORE

    IF game_data.game_mode == "race"
        OUTPUT 0 0 font_size DOUBLE(fontSize / 1.5)
    ELSE
        OUTPUT 0 0 font_size fontSize
    END IF
    OUTPUT 0 0 image bg_svg
    OUTPUT 0 0 image_width width
    OUTPUT 0 0 image_height height
    OUTPUT 0 0 width width
    OUTPUT 0 0 height height
    OUTPUT 0 0 font font
    OUTPUT 0 0 indent indent
    IF shadow
        OUTPUT 0 0 shadow 1
    END IF
    IF align == "flex-start"
        OUTPUT 0 0 align "left-edge"
    END IF
    IF align == "flex-end"
        OUTPUT 0 0 align "right-edge"
    END IF

    STRING main_score_bg
    main_score_bg = bgCCustom

    IF bgC == "team"
        main_score_bg = team_color
    END IF
    OUTPUT 0 0 image_color main_score_bg

    STRING main_score_grayscale
    main_score_grayscale = GRAYSCALE main_score_bg

    IF main_score_grayscale > 0.55
        OUTPUT 0 0 color "#1c1c1c"
        OUTPUT 0 1 color "#1c1c1c"
    ELSE 
        OUTPUT 0 0 color "#ffffff"
        OUTPUT 0 1 color "#ffffff"
    END IF
    
    IF sType == "score"
        OUTPUT 0 0 value main_score
    ELSE
        IF game_data.round_mode == "false" && !IN_EDITOR
            discard
        END IF

        OUTPUT 0 0 value round_score
    END IF

    # ====================================
    # ROUND SCORE
    INT show_round_score
    show_round_score = 0
    IF rS && IN_EDITOR
        show_round_score = 1
    END IF
    IF rS && game_data.round_mode
        show_round_score = 1
    END IF
    IF game_data.game_mode == "wipeout" || game_data.game_mode == "ca"
        show_round_score = 0
    END IF
    IF game_data.game_mode == "extinction"
        show_round_score = 0
    END IF

    IF show_round_score
        OUTPUT 0 1 width width
        OUTPUT 0 1 height 3
        OUTPUT 0 1 x 0
        OUTPUT 0 1 y 2.9
        OUTPUT 0 1 font "roboto-bold"
        OUTPUT 0 1 font_size 2.4
        OUTPUT 0 1 value round_score
        OUTPUT 0 1 fill team_color_dark
        OUTPUT 0 1 font "roboto-bold"
    ELSE
        OUTPUT 0 1 opacity 0
    END IF

    # FLAG ============================================
    INT show_flag_state
    show_flag_state = 0
    IF fS && IN_EDITOR
        show_flag_state = 1
    END IF
    IF game_data.game_mode == "ctf" && fS
        show_flag_state = 1
    END IF

    IF show_flag_state
        DOUBLE flag_offset
        flag_offset = height + 1

        IF show_round_score
            flag_offset = height + 3.5
        END IF

        OUTPUT 0 2 width width
        OUTPUT 0 2 height height
        OUTPUT 0 2 image_width width
        OUTPUT 0 2 image_height height
        OUTPUT 0 2 align "center"
        OUTPUT 0 2 image_color "#ffffff
        OUTPUT 0 2 x 0
        OUTPUT 0 2 y 0
        OUTPUT 0 2 offset_y flag_offset

        IF flag_state == 0
            OUTPUT 0 2 image "images/item_flag.svg"
            OUTPUT 0 2 hue team_color
        END IF
        IF flag_state == 1
            OUTPUT 0 2 image "images/icons/fa/times.svg"
            OUTPUT 0 2 image_color "EE3015"
        END IF
        IF flag_state == 2
            OUTPUT 0 2 image "images/icons/fa/question.svg"
            OUTPUT 0 2 image_color "EEEEEE"
        END IF
    END IF

    INT show_macguffin_state
    show_macguffin_state = 0
    IF mS && IN_EDITOR
        show_macguffin_state = 1
    END IF
    IF game_data.game_mode == "macguffin" && mS
        show_macguffin_state = 1
    END IF

    IF show_macguffin_state

        #============================================
        # MACGUFFIN PROGRESS BAR 
        OUTPUT 1 0 image macguffin_bar
        OUTPUT 1 0 image_width 2.5
        OUTPUT 1 0 image_height height
        OUTPUT 1 0 image_color team_color
        OUTPUT 1 0 width 2.5
        OUTPUT 1 0 height height
        OUTPUT 1 0 fill "#00000066"
        OUTPUT 1 0 x macguffin_offset
        OUTPUT 1 0 y 0
        OUTPUT 1 0 clip_y DOUBLE (main_score / 100)

        #============================================
        # MACGUFFIN
        OUTPUT 1 1 image "images/item_macguffin.svg"
        OUTPUT 1 1 width 2.5
        OUTPUT 1 1 height 2.5
        OUTPUT 1 1 image_width 3
        OUTPUT 1 1 image_height 3
        OUTPUT 1 1 x macguffin_offset
        OUTPUT 1 1 y 3.6
        OUTPUT 1 1 image_color "#f8d206"

        IF has_macguffin
            OUTPUT 1 0 opacity 1
            OUTPUT 1 0 GRAYSCALE 0
            OUTPUT 1 1 opacity 1
        ELSE 
            OUTPUT 1 0 opacity 0.6
            OUTPUT 1 0 GRAYSCALE 0.6
            OUTPUT 1 1 opacity 0
        END IF
    ELSE
        OUTPUT 1 0 opacity 0
        OUTPUT 1 1 opacity 0
    END IF

END TEMPLATE